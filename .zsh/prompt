# vim: ft=zsh

function precmd {

    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))

    ###
    # Truncate the path if it's too long.
    
    PR_FILLBAR=""
    PR_PWDLEN=""
    
    local promptsize=${#${(%):-[%n@%m] }}
    local pwdsize=${#${(%):- [%~]}}
    
    if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
	    ((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
        PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi
    print -Pn '\e]0;$(basename $SHELL)\a'
}

setopt extended_glob
preexec () {
    if [[ "$TERM" == "screen" ]]; then
        local CMD=${1[(wr)^(*=*|sudo|-*)]}
		if [[ "$CMD" == "zsh" ]]; then
            CMD=short_pwd
        fi
        echo -n "\ek$CMD\e\\"
    else
        print -Pn "\e]0;${1}\a"
    fi
}


setprompt () {
    ###
    # Need this so the prompt will work.

    setopt prompt_subst

    ###
    # See if we can use colors.

    autoload colors zsh/terminfo
    if [[ "$terminfo[colors]" -ge 8 ]]; then
        colors
    fi
    for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE BLACK; do
        eval PR_BOLD_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
        eval PR_$color='%{$fg[${(L)color}]%}'
    done
    PR_BOLD="%{$terminfo[bold]%}"
    PR_NO_COLOUR="%{$terminfo[sgr0]%}"

    ###
    # See if we can use extended characters to look nicer.
    
    PR_HBAR='-'

    ###
    # Decide if we need to set titlebar text.
    
    case $TERM in
	xterm*)
	    PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
	    ;;
	screen)
	    PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
	    ;;
	*)
	    PR_TITLEBAR=''
	    ;;
    esac
    
    
    ###
    # Decide whether to set a screen title
    if [[ "$TERM" == "screen" ]]; then
        PR_STITLE=$'%{\ekzsh\e\\%}'
    else
        PR_STITLE=''
    fi
    
    ###
    # Finally, the prompt.

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
[%(!.${PR_RED}.${PR_YELLOW})%n${PR_NO_COLOUR}@${PR_GREEN}%m${PR_NO_COLOUR}]%(!.${PR_RED}.${PR_CYAN})\
 ${(e)PR_FILLBAR} ${PR_NO_COLOUR}\
[${PR_BOLD}%${PR_PWDLEN}<...<%~%<<${PR_NO_COLOUR}]\

%(!.${PR_RED}%#.>)$PR_NO_COLOUR '

#    RPROMPT=' ${PR_CYAN}%D{%H:%H %a, %d/%m}$PR_NO_COLOUR'

    PS2='$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_BLUE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT(\
$PR_LIGHT_GREEN%_$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT$PR_NO_COLOUR '
}

setprompt
